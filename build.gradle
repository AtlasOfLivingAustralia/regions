buildscript {
    repositories {
        mavenLocal()
        maven { url "https://nexus.ala.org.au/content/groups/public/" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:$webdriverBinariesVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:3.4.6"
    }
}

plugins {
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
}


version "3.6.0-SNAPSHOT"
group "au.org.ala"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"war"
apply plugin:"org.grails.grails-web"
apply plugin:"com.github.erdi.webdriver-binaries"
apply plugin:"org.grails.grails-gsp"
apply plugin:"com.bertramlabs.asset-pipeline"
apply plugin:"maven-publish"


publishing {
    targetCompatibility = 1.11
    repositories {
        maven {
            name 'Nexus'
            url "https://nexus.ala.org.au/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
            credentials {
                username = System.getenv('TRAVIS_DEPLOY_USERNAME')
                password = System.getenv('TRAVIS_DEPLOY_PASSWORD')
            }
        }
    }
    publications {
        mavenJar(MavenPublication) {
            artifact bootWar
        }
    }
}

repositories {
    mavenLocal()
    maven { url "https://nexus.ala.org.au/content/groups/public/" }
    maven { url "https://repo.grails.org/grails/core" }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    compileOnly "org.springframework.boot:spring-boot-starter-tomcat"

    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.grails:grails-core"
    implementation "org.grails:grails-dependencies"
    implementation "org.grails:grails-web-boot"
    implementation "org.grails:grails-logging"
    implementation "org.grails.plugins:cache"
    implementation "org.grails.plugins:cache-ehcache:3.0.0"
    console "org.grails:grails-console"
    implementation "org.grails:grails-datastore-gorm:${gormVersion}"
    implementation 'dk.glasius:external-config:3.1.1'
    implementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'

    testImplementation "org.grails.plugins:geb"
    testImplementation "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
    testImplementation "org.seleniumhq.selenium:selenium-safari-driver:$seleniumSafariDriverVersion"

    testImplementation  "org.seleniumhq.selenium:selenium-remote-driver:$seleniumVersion"
    testImplementation  "org.seleniumhq.selenium:selenium-api:$seleniumVersion"
    testImplementation  "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
    testImplementation  "org.grails:grails-web-testing-support"
    profile "org.grails.profiles:web"

    runtimeOnly "com.bertramlabs.plugins:asset-pipeline-grails:3.4.6"
    implementation 'com.nerderg.ajaxanywhere:ajaxanywhere:2.1.1'
    implementation "net.sf.ehcache:ehcache-core:2.6.11"
    implementation "commons-httpclient:commons-httpclient:3.1"

    implementation "org.grails.plugins:ala-auth:$alaSecurityLibsVersion"
    implementation "org.grails.plugins:ala-admin-plugin:2.3.0"
    implementation  "org.grails.plugins:ala-bootstrap3:4.1.0"
}

webdriverBinaries {
    chromedriver "$chromeDriverVersion"
    geckodriver "$geckodriverVersion"
}

tasks.withType(Test) {
    systemProperty "geb.env", System.getProperty('geb.env')
    systemProperty "geb.build.reportsDir", reporting.file("geb/integrationTest")
    systemProperty "webdriver.chrome.driver", System.getProperty('webdriver.chrome.driver')
    systemProperty "webdriver.gecko.driver", System.getProperty('webdriver.gecko.driver')
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

bootRun {
    ignoreExitValue true
    jvmArgs(
            '-Dspring.output.ansi.enabled=always',
            '-noverify',
            '-XX:TieredStopAtLevel=1',
            '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

bootWar {
    launchScript()
}

assets {
    minifyJs = true
    minifyCss = true
}
